**************************************************************************************************************
Creating base stimuli 
**************************************************************************************************************
<item ArcPicture>
 / 3 = "Arc6.png"
 / 4 = "Arc8.png"
 / 5 = "Arc10.png"
 / 6 = "Arc12.png"
 / 7 = "Arc14.png"
 / 8 = "Arc16.png"
 / 9 = "Arc18.png"
 / 10 = "Arc20.png"
 / 11 = "Arc22.png"
 / 12 = "Arc24.png"
 / 13 = "Arc26.png"
 / 14 = "Arc28.png"
 / 15 = "Arc30.png"
 / 16 = "Arc32.png"
 / 17 = "Arc34.png"
 / 18 = "Arc36.png"
 / 19 = "Arc38.png"
 / 20 = "Arc40.png"
 / 21 = "Arc42.png"
 / 22 = "Arc44.png"
 / 23 = "Arc46.png"
 / 24 = "Arc48.png"
 / 25 = "Arc50.png"
 / 26 = "Arc52.png"
 / 27 = "Arc54.png"
 / 28 = "Arc56.png"
 / 29 = "Arc58.png"
 / 30 = "Arc60.png"
</item>
 
 <item cannonmark>
/1 = "CannonTickMark.png"
/ 2 = "Fixation.png"
</item>

<item Cannon>
 / 1 = "cannon.png"
 / 2 = "Fixation.png"
</item>

**************************************************************************************************************
Creating image stimuli 
**************************************************************************************************************

<picture shield>
/ items = ArcPicture
/ size  = (500px, 500px)
/ position = (50%,50%)
/ rotation = (values.placementAngle)
/ erase = false
</picture>

<picture keyboard_diagram_reference>
/ items = ("keyboard_demo.svg")
/ size  = (300px, 300px))
/ position = (50%,85%)
/ rotation = 0
</picture>

<picture shield_center>
/ items = ("Arc1.png")
/ size = (500px, 500px))
/ position = (50%, 50%)
/ rotation = values.Placementangle
/ erase = false
</picture>

<picture wall>
/ items = ("Arc0.png")
/ size = (500px, 500px))
/ position = (50%, 50%)
/ rotation = values.Placementangle
/ erase = false
</picture>


<shape responsearea>
/ shape = circle
/ size = (435px, 435px)
/ position = (50%, 50%)
/ color = white
/ erase = false
</shape>


<picture cannon>
/ items = Cannon
/ size = (270px, 270px)
/ position = (50%, 50%)
/ rotation = values.angmu
/ select = values.cannon
/ erase = false
</picture>

<picture cannonball>
/ items  = ("cannonball.png")
/ size = (470px, 470px)
/ position = (50%, 50%)
/ rotation = values.outcome
/ erase = false
</picture>

**************************************************************************************************************
Creating Text stimuli for instructions
**************************************************************************************************************

<item Instructionsshield>
/ 1 = "You will know the cannon is ready to fire when you see the 
~n CANNON READY signal appear in the center of the screen"
/ 2 = "The green tick mark at the top of the screen represents the center of your shield. 
~n you can move the shield with your mouse."
/ 3 = "To place your shield, you should click the portion of the wall where the cannon is 
~n aiming with your mouse. This will place your shield in the cannonball's path." 
/ 4 = "You should place your shield as quickly and accurately as possible 
~n once the ''CANNON READY'' signal appears on the screen"
</item>

<text Instructions_shield>
/ items = Instructionsshield
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 15%)
/ size = (80%, 20%)
/ valign = center
/ halign = center
/ vjustify = center
/ select = values.instructshield
</text>


<item Practice_ShieldSize>
/1 = "You can earn money in this task by catching cannonballs in your shield. 
~n You will earn <%values.centsearned%> cents each time you catch the ball. 
~n If you miss you will not earn anything."
/2 = "On some trials the shield will be large and on some trials it will be small. 
~n You cannot know the size of the shield until the cannon is fired, 
~n so it is best to try to catch the ball on every trial."
/3 = "You'll have some practice now to get a sense of how the shield size varies." 
</item>

<text Practice_ShieldSize>
/ items = Practice_ShieldSize
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ valign = center
/ halign = center
/ vjustify = center
/ select = sequence
</text>

<text PracticeBlock_CannonMoves>
/ items = ("Up until now, the cannon has usually aimed at the same location. 
~n Now, the cannon will occasionally re-aim to a completely different part of the circle. 
~n To earn the most points, you should try your best to center your shield where the cannon is aimed.")
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ valign = center
/ halign = center
/ vjustify = center
/ select = sequence
</text>

<item CannonNotAccurate>
/1= "While the cannon has been pretty accurate so far in practice, the real cannon is not perfectly accurate. 
~n On some trials it might shoot a bit above where it is aimed and on other trials a bit below." 
/2 = "~n Your best strategy is still to place the shield in the location where the cannon is aimed. 
~n The cannon can still re-aim unpredictably and shots might be fired in any direction."
</item>

<text PracticeBlock_CannonNotAccurate>
/ items = CannonNotAccurate
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ hjustify = center
/ vjustify = center
/ select = sequence
</text>

<item Practice_CannonNotVisible>
/1 =  "The cannon is still aiming and shooting exactly as before. 
~n You will be paid for catching balls exactly as before, 
~n but now you must place your shield at the position where you think the cannon is aimed."
/2 = "Since you will still see each ball shot from the cannon, 
~n you will be able to use the locations of past shots to inform your decision."
</item>

<text PracticeBlock_CannonNotVisible>
/ items = Practice_CannonNotVisible
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ hjustify = center
/ vjustify = center
/ select = sequence
</text>

<item warning_text>
/ 1 = "CANNON READY"
/ 2 = "" 
</item>
<text cannon_ready>
/ items = warning_text
/ fontstyle = ("Arial", 2%, true, false, false, false, 5, 1)
/ position = (50%, 65%)
/ select = values.warning_text
/ valign = center
/ halign = center
/ color = red
/ txbgcolor = white

</text>

<text Spacetocontinue>
/ items = ("Press SPACE to continue")
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 85%)
/ valign = center
/ halign = center
</text>

<item lockin>
/1 = "Practice catching the cannonballs now. Place the green tick mark on the part of the circle where the cannon is aimed.
~n Your mouse click will lock in your shield position"
/99 = "Now see what happens when you miss the ball. Move the green tick mark AWAY from the part of the circle where the cannon is aimed.
~n Your mouse click will lock in your shield position"

</item>
<text Lockin>
/ items = lockin
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 85%)
/ size = (80%, 20%)
/ valign = center
/ halign = center
/ select = sequence
</text>

<text ValueTracker>
/ items = ("angmu = <%values.angmu%>
~n cannonball rotation = <%picture.cannonball.rotation%>
~n block earnings = <%values.blockearnings%>
~n cannonballs caught <%values.cannonballs_caught%>

")
/ fontstyle = ("Arial", 2%, true, false, false, false, 5, 1)
/ position = (80%, 80%)
/ size = (15%, 5%)
</text>

**************************************************************************************************************
Initializing lists for probabilistic selection
**************************************************************************************************************
<list StayorSwitch> // Determining whether to stay or switch 
/ items = (0, 1) // 1 = switch, 0 = stay
/ poolsize = 100
/ selectionmode = random
/ itemprobabilities = (.86,.14)
</list>

<list Condition>
/ items = (1, 2) // 1 = oddball, 2 = changepoint
/ poolsize = 2
/ selectionmode = random
</list>

<list previousshield>
/ items = (0) // 1 = oddball, 2 = changepoint
/ poolsize = 1
</list>

**************************************************************************************************************
Values which will be updated during the trials
**************************************************************************************************************	    
<values>
/ StaySwitch = 0 // value represented whether cannon should stay in same spot(0) or switch to a new one(1)):
/ percentTrialsStay = 0.86
/ ang = 0 // angle of cannonball strike, updated throughout the task (0-360)
/ practiceblock = 1 // update through various phases (1-5 are practice or training conditions, 0 is )
/ hitmiss = 0
/ responseTime = ""
/ currentArcitem = 1
/ centsearned = .05
/ totalearnings = 0
/ angmu = 0
/ outcomeindex = 0 
/ cannon = 1
/ warning_text = 2
/ instructshield = 1
/ cannonballs_caught = 0
/ cannonballs_missed = 0
/ rand = 0
/trialnum = 0
/ conc = 18.775
/ experimentname = "Cannonball Task"
/ blocknum = 0
/pacetocontinue = 1
/ block = 0
/outcome = 0
/blockearnings = 0


//values for calculating response angle
/ anglesize = 0

/ selected_x = 0
/ selected_y = 0
/ quadrant = 0
/ oppositeSide = 0
/ adjacentSide = 0
/ hypotenuse = 0
/ oppositeSideAngle_degrees = 0
/ oppositeSideAngle_radians = 0
/ placementAngle = 0
/ valid = 0
</values>
<parameters>
/ ChoiceTime = 4000 //how long pts have to make choice
/ circleproportion = 0.25
/ step_deg = 2
/ step_deg_lg = 15
</parameters>
**************************************************************************************************************
Useful expressions
**************************************************************************************************************	 
<expressions>
/percentTrialsSwitch = 1-values.percentTrialsStay
/ circleproportion = 0.25
/ angleup = values.placementAngle+ picture.shield.currentitemnumber
/ angledown = values.placementAngle- picture.shield.currentitemnumber
/ angoddball = randgaussian(0, 2.99)
/ centsearned = values.cannonballs_caught*values.centsearned
/ outcome = randgaussian(values.angmu, values.conc)
/ angsurprise = rand(0, 360)
//Expressions for calculating angles
/ centerx = 0.5*display.canvaswidth
/ centery = 0.5*display.canvasheight
/ calculateSelectedPosition = {

	if (values.selected_x > expressions.centerx && values.selected_y <= expressions.centery){
		values.quadrant = 1;
	} else if (values.selected_x >= expressions.centerx && values.selected_y > expressions.centery){
		values.quadrant = 2;
	} else if (values.selected_x < expressions.centerx && values.selected_y >= expressions.centery){
		values.quadrant = 3;
	} else if (values.selected_x <= expressions.centerx && values.selected_y < expressions.centery){
		values.quadrant = 4;
	} else {
		values.quadrant = "check data";
	};
	
	if (values.quadrant == 1){
		if (values.selected_y == expressions.centery){
			values.oppositeSideAngle_degrees = 90;
		} else {
			values.oppositeSide = abs(values.selected_x - expressions.centerx);
			values.adjacentSide = abs(values.selected_y - expressions.centery);
			values.hypotenuse = sqrt(values.oppositeSide*values.oppositeSide + values.adjacentSide*values.adjacentSide);
			values.oppositeSideAngle_radians = asin(values.oppositeSide/values.hypotenuse);
			values.oppositeSideAngle_degrees = deg(values.oppositeSideAngle_radians);			
		};
		values.placementAngle = values.oppositeSideAngle_degrees + 0;
	} else if (values.quadrant == 2){
		if (values.selected_x == expressions.centerx){
			values.oppositeSideAngle_degrees = 90;
		} else {
			values.oppositeSide = abs(values.selected_y - expressions.centery);
			values.adjacentSide = abs(values.selected_x - expressions.centerx);
			values.hypotenuse = sqrt(values.oppositeSide*values.oppositeSide + values.adjacentSide*values.adjacentSide);
			values.oppositeSideAngle_radians = asin(values.oppositeSide/values.hypotenuse);
			values.oppositeSideAngle_degrees = deg(values.oppositeSideAngle_radians);			
		};
		values.placementAngle = values.oppositeSideAngle_degrees + 90;		
	} else if (values.quadrant == 3){
		if (values.selected_y == expressions.centery){
			values.oppositeSideAngle_degrees = 90;
		} else {
			values.oppositeSide = abs(values.selected_x - expressions.centerx);
			values.adjacentSide = abs(values.selected_y - expressions.centery);
			values.hypotenuse = sqrt(values.oppositeSide*values.oppositeSide + values.adjacentSide*values.adjacentSide);
			values.oppositeSideAngle_radians = asin(values.oppositeSide/values.hypotenuse);
			values.oppositeSideAngle_degrees = deg(values.oppositeSideAngle_radians);			
		};
		values.placementAngle = values.oppositeSideAngle_degrees + 180;		
	} else if (values.quadrant == 4){
		if (values.selected_x == expressions.centerx){
			values.oppositeSideAngle_degrees = 90;
		} else {
			values.oppositeSide = abs(values.selected_y - expressions.centery);
			values.adjacentSide = abs(values.selected_x - expressions.centerx);
			values.hypotenuse = sqrt(values.oppositeSide*values.oppositeSide + values.adjacentSide*values.adjacentSide);
			values.oppositeSideAngle_radians = asin(values.oppositeSide/values.hypotenuse);
			values.oppositeSideAngle_degrees = deg(values.oppositeSideAngle_radians);			
		};
		values.placementAngle = values.oppositeSideAngle_degrees + 270;		
	} else {
		values.placementAngle = 360;
	};
}

</expressions>
**************************************************************************************************************
Output data file columns
**************************************************************************************************************
<data>
/ columns = (build, 
values.experimentName, 
subject, 
date, 
time, 
blockcode,
trialcode,	 // General information
values.block, 
values.StaySwitch, 
values.percentTrialsStay,
values.conc, 
values.trialnum, 
values.outcomeindex, 
values.practiceblock, 
values.cannon, 
values.hitmiss,
values.ang,
values.angmu,
values.outcome, 
values.cannonballs_caught, 
values.cannonballs_missed, 
values.placementAngle, 
values.totalearnings, 
values.blockearnings,
block.InstructionBlock.timestamp,
trial.placeshield_mouse.latency,
trial.begin_block.timestamp,
trial.mainloop.timestamp,
trial.placeshield_mouse.timestamp,
trial.showPE.timestamp, 
trial.cannon_outcome.timestamp,
picture.shield.currentitem,
list.previousshield.currentitem,
expressions.angleup, 
expressions.angledown,
) // Block level variables		

</data>
**************************************************************************************************************
Instructions Trials
**************************************************************************************************************	  

<text Instructions_cannon>
/ items = ("In this task, a cannon is aimed at the circle. Your goal is to block the cannon's shot and keep the cannonball from getting over the wall.")
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1), 
/ position = (50%, 15%)
/ size = (90%, 15%)
/ valign = center
/ halign = center
/ vjustify = center
/ select = sequence
</text>

<trial Instructions_BeginTask>
/ stimulusframes = [1 = wall, shield_center, Instructions_cannon, cannon, Spacetocontinue]
/ validresponse = (57)
/ beginresponsetime = 500
/ ontrialbegin = [
	if (values.practiceblock == 5 || values.practiceblock == 0) {
		values.cannon = 2;
	} else {
		values.cannon = 1;
	};
	values.StaySwitch = 0;
	values.block = 0;
	values.ang = 0;
	values.practiceblock = 1;
	]
/ branch = [
	return trial.Instructions_PlaceShield_delay;
]

</trial>


<trial Instructions_PlaceShield_delay>
/ stimulusframes = [1 = cannon, wall, shield_center, Instructions_shield, Spacetocontinue, cannon_ready]
/ timeout = 15000
/ correctresponse = (57)
/ ontrialbegin = [
	if (values.instructshield == 4) {
		values.warning_text = 1;
	}
]
/ ontrialend = [
	values.instructshield += 1
]
/ branch = [
	if(values.instructshield <= 4) {
		return trial.Instructions_PlaceShield_delay;
	} 
	
]
</trial>

<trial Instructions_PlaceShield>
/ stimulusframes = [1 = responsearea, cannon, wall, shield_center, cannon_ready, Lockin, Instructions_shield]
/ inputdevice = mouse
/ correctresponse = (responsearea)
/ ontrialbegin = [
	picture.shield_center.rotation = 20;
]
/ontrialend = [
    values.selected_x = 1px*trial.Instructions_PlaceShield.responsex;
	values.selected_y = 1px*trial.Instructions_PlaceShield.responsey;
	expressions.calculateSelectedPosition;
	list.previousshield.appenditem(values.placementAngle);
	]

/ branch = [
	if(trial.Instructions_PlaceShield.response == "responsearea") {
		picture.shield_center.rotation = values.placementAngle;
        return trial.Instructions_CannonOutcome;
	} else if (trial.Instructions_PlaceShield.response == "responsearea") {
		return trial.Instructions_PlaceShield;
		}
]

</trial>


<trial Instructions_CannonOutcome>
/ stimulusframes = [1 = cannon, wall, shield_center, cannonball]
/ correctresponse = (57)
/ trialduration = 2000
/ beginresponsetime = 1000
/ branch = [
	if (values.outcome <= (values.placementAngle + 1) && values.outcome >= (values.placementAngle - 1)) {
		values.hitmiss = 1;	
		if (values.practiceblock == 1) {
			values.outcomeindex = 3;
			values.instructshield = 1
		};
		trial.cannon_outcome;
	} else if (values.placementAngle + 10 >= 360){
		if (values.outcome <= (values.placementAngle + 10 - 360)) {
			values.hitmiss = 1;	
			if (values.practiceblock == 1) {
				values.outcomeindex = 3;
			};
			trial.cannon_outcome;
		}
	} else if (values.placementAngle - 10 < 0){
		if (values.ang >= (values.placementAngle - 10 + 360)) {
			values.hitmiss = 1;	
			if (values.practiceblock == 1) {
				values.outcomeindex = 3;
			};
			trial.cannon_outcome;
			}
	} else {
		values.hitmiss = 0;
		if (values.practiceblock == 1){
			values.outcomeindex = 2;
		};
		trial.cannon_outcome;
	}
		
]
</trial>

<trial Instructions_RevealShield>
/ stimulusframes = [1 = Practice_ShieldSize, Spacetocontinue]
/ correctresponse = (57)
/ ontrialbegin = [
	 values.practiceblock = 2
	 
]
</trial>


<trial PracticeBlock_CannonMoves>
/ stimulusframes = [1 = PracticeBlock_CannonMoves, Spacetocontinue]
/ correctresponse = (57)
/ ontrialend = [
	values.practiceblock = 3;
]
</trial>

<trial PracticeBlock_CannonNotAccurate>
/ stimulusframes = [1 = PracticeBlock_CannonNotAccurate, Spacetocontinue]
/ correctresponse = (57)
/ ontrialend = [
	values.practiceblock = 3;
	values.block = 2;
]
</trial>

<trial PracticeBlock_CannonNotVisible>
/ stimulusframes = [1 = PracticeBlock_CannonNotVisible, Spacetocontinue]
/ correctresponse = (57)
/ ontrialend = [
	values.practiceblock = 5;
	values.cannon = 2;
	values.trialnum += 1;
	values.angmu = expressions.angsurprise;
	
]
/ branch = [
	return trial.mainloop;
]

</trial>

<item EndPractice>
/1 = "You have completed the practice block and earned <%values.blockearnings%>!
~n now that you understand the game, you will play a few more rounds to earn more money."
/2 = "In each round, the cannon will change how it fires. 
~n You'll get a few practice trials at the beginning of each round to learn about this."
/3 = "You'll want to figure out the best strategy each round 
~n to catch the cannonballs and maximize your earnings.
~n Good Luck!"

</item>

<text EndPractice>
/ items = EndPractice
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ hjustify = center
/ vjustify = center
/ select = sequence
</text>


<trial EndPractice>
/ stimulusframes = [1 = EndPractice, Spacetocontinue]
/ correctresponse = (57)
/ ontrialbegin = [
	values.blockearnings = values.cannonballs_caught*values.centsearned;
	values.totalearnings = values.blockearnings + values.totalearnings;
]
/ ontrialend = [
	values.practiceblock = 4;
	values.block = 2;
	values.trialnum = 0;
	values.cannonballs_caught = 0;
	values.cannonballs_missed = 0;
	
]
</trial>

<text Reminder>
/ items = ("Remember, you should try your best to place your shield at the center of the cannon's aim.
 ~n Even if you cannot see the cannon, take your best guess where it is aiming based on the cannonballs you've seen before.") 
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ hjustify = center
/ vjustify = center
</text>


<trial Reminder>
/ stimulusframes = [1 = Reminder, Spacetocontinue]
/ correctresponse = (57)
/ branch = [
	trial.mainloop
]
</trial>

***************************************************************************************************
Experimental Block instructions
***************************************************************************************************

<item start>
/ 1 = "This is the beginning of the ODDBALL ROUND. 
~nDuring this block you will earn <%values.centsearned%> for each cannonball you catch." 
/2 = "This is the beginning of the CHANGE POINT ROUND. 
~nDuring this block you will earn <%values.centsearned%> for each cannonball you catch." 
</item>

<text block_start>
 /items = start
 / fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ hjustify = center
/ vjustify = center
/ select = values.block
</text>


<trial begin_block> 
/ stimulusframes = [1 = block_start, Spacetocontinue]
/ correctresponse = (57)
/ recorddata = false
/ ontrialbegin = [
	values.blockearnings = values.cannonballs_caught*values.centsearned;
	values.totalearnings = values.totalearnings + values.blockearnings;
	values.block = list.Condition.nextvalue;
	values.angmu = expressions.angsurprise;

	values.practiceblock = 4;
	if (values.block == 2) {
		text.block_start.currentitemnumber == 2
	} else if (values.block == 1) {
		text.block_start.currentitemnumber == 1
	};
]
/ branch = [
	return trial.cannonisaimed
]

</trial>


<item start_task>
/ 1 = "You've successfully completed training and are ready to start.
~n you earned <%values.blockearnings%> cents in this training round.
~n Now the ODDBALL ROUND will begin. 
~n Good Luck!" 
/ 2 = "You've successfully completed training and are ready to start. 
~n you earned <%values.blockearnings%> cents in this training round.
~n Now the CHANGE POINT ROUND will begin.
~n Good Luck!" 
</item>


<text start_task>
 /items = start_task
 / fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ hjustify = center
/ vjustify = center
/ select = values.block
</text>



<trial start_task> 
/ stimulusframes = [1 = start_task, Spacetocontinue]
/ correctresponse = (57)
/ recorddata = false
/ ontrialbegin = [
	values.blockearnings = values.cannonballs_caught*values.centsearned;
]
/ ontrialend = [
	values.practiceblock = 0;
	values.totalearnings = values.totalearnings + values.blockearnings;
	values.cannonballs_caught = 0;
	values.cannonballs_missed = 0;
	values.angmu = expressions.angsurprise;
]
</trial>

<text cannonisaimed>
 / items = ("Just like before, on each trial a cannon will aim at a location on the circle.")
 / fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ hjustify = center
/ vjustify = center
</text>

<trial cannonisaimed> 
/ stimulusframes = [1 = cannonisaimed, Spacetocontinue]
/ correctresponse = (57)
/ recorddata = false
/ branch = [
	return trial.description
]
</trial>

<item description>
/1 = "On most trials the cannon will fire a ball somewhere near the point of aim.
~n However, on a few trials there will be a shot from a random direction
~n that is equally likely to hit any location on the circle."
/2 = "On all trials the cannon will fire a ball somewhere near the point of aim. 
~nMost of the time the cannon will remain aimed at the same location, 
~n but occasionally the cannon will be reaimed."
</item>


<text description>
 /items = description
 / fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ hjustify = center
/ vjustify = center
/ valign = center
/ select = values.block
</text>

<trial description> 
/ stimulusframes = [1 = description, Spacetocontinue]
/ correctresponse = (57)
/ recorddata = false
/ branch = [
	return trial.youwillnotsee
]
</trial>


<item youwillsee>
/1 = "In this first practice, you will see the cannon. Try to place 
 ~n your shield where it is aimed in order to catch balls and earn money.
 ~n
~n press SPACE to start the practice round"
/2 = "In this next practice session you will not see the cannon, 
 ~n but still have to infer its aim in order to catch balls and earn money.
 ~n
~n press SPACE to start the practice round"


</item>

<text youwillnotsee>
/items = youwillsee
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ hjustify = center
/ vjustify = center
/ select = sequence
</text>

<trial youwillnotsee> 
/ stimulusframes = [1 = youwillnotsee]
/ correctresponse = (57)
/ recorddata = false
/ branch = [
	if (values.practiceblock == 5){
		return trial.PracticeBlock_CannonNotVisible;
	} else {
		return trial.mainloop
	};
]
</trial>


**************************************************************************************************************
Core Task Trials
**************************************************************************************************************	  
//switch oddball for changepoint

<trial mainloop>
/ stimulusframes = [1 = wall]
/ trialduration = 100
/ recorddata = true   
/ ontrialbegin = [ 
if (values.practiceblock == 1) {
	// practice hit
	values.StaySwitch = 0;
	values.angmu = 0; //cannon
	values.outcome = 0; //cannonball
} else if (values.practiceblock == 2) { // practice shield size
	values.StaySwitch = 0;
	values.angmu = 0; //cannon
	values.outcome = 0; //cannon
} else if (values.practiceblock == 3) { // practice movement
	values.StaySwitch = 0;
	values.angmu = expressions.angsurprise; //cannon
	values.outcome = values.angmu; //cannonball
} else if (values.practiceblock == 4){
	// practice cannon visible
   values.StaySwitch = list.StayorSwitch.nextvalue;
   if (values.StaySwitch == 1){ //switch
	   if (values.block == 1) { //oddball 
			values.outcome = expressions.angsurprise; //cannonball	
		} else if (values.block == 2) { //Changepoint
			values.angmu = expressions.angsurprise; //cannon
			values.outcome = expressions.outcome; //cannonball
			values.outcome = expressions.outcome; //cannonball
			}
   } else if (values.StaySwitch == 0) {
	   //stay
		if (values.block == 1){ //Oddball
			values.angmu = values.angmu + expressions.angoddball; //adding random walk to cannon
			values.outcome = expressions.outcome; //cannonnball location
		} else if (values.block == 2) {
			values.outcome = expressions.outcome; // cannnonball
			}
   }
} else if (values.practiceblock == 5) {
	// practice cannon not visible
	values.cannon = 2;
	values.StaySwitch = list.StayorSwitch.nextvalue;
    if (values.StaySwitch == 1){ //switch
		if (values.block == 1) { //oddball
			values.outcome = expressions.angsurprise; //cannonball	
		} else if (values.block == 2) { 
			values.angmu = expressions.angsurprise; //cannon
			values.outcome = expressions.outcome; //cannonball
			} 
		} else if (values.StaySwitch == 0) {
		if (values.block == 1){ //Oddball
			values.angmu = values.angmu + expressions.angoddball; //adding random walk to cannon
			values.outcome = expressions.outcome; //cannonnball location
		} else if (values.block == 2) {
			values.outcome = expressions.outcome; // cannnonball
			}
		}
} else if (values.practiceblock == 0) {
	values.cannon = 2;
	values.trialnum += 1;
	values.StaySwitch = list.StayorSwitch.nextvalue;
    if (values.StaySwitch == 1){ //switch
		if (values.block == 1) { //oddball
			values.outcome = expressions.angsurprise; //cannonball	
		} else if (values.block == 2) { 
			values.angmu = expressions.angsurprise; //cannon
			values.outcome = expressions.outcome; //cannonball
			} 
		} else if (values.StaySwitch == 0) {
		if (values.block == 1){ //Oddball
			values.angmu = values.angmu + expressions.angoddball; //adding random walk to cannon
			values.outcome = expressions.outcome; //cannonnball location
		} else if (values.block == 2) {
			values.outcome = expressions.outcome; // cannnonball
			}
		}
	}; 
	picture.cannon.rotation = values.angmu;
	picture.cannonball.rotation = values.outcome;
]
/ branch = [
	trial.placeshield_mouse;	
]
</trial>


<trial placeshield_mouse>
/ stimulusframes = [1 = responsearea, wall, cannon, cannon_ready]
/ inputdevice = mouse
/ validresponse = (responsearea)
/ correctresponse = (responsearea)
/ ontrialbegin = [
	if (values.practiceblock == 5 || values.practiceblock == 0) {
		values.cannon = 2;
	} else if (values.practiceblock == 4) {
		values.cannon = 1;
	} else {
		values.cannon = 1;
	};
]

/ontrialend = [
    values.selected_x = 1px*trial.placeshield_mouse.responsex;
	values.selected_y = 1px*trial.placeshield_mouse.responsey;
	expressions.calculateSelectedPosition;
	values.placementAngle = values.placementAngle;
	list.previousshield.appenditem(values.placementAngle);
	]
/ branch = [
	if(trial.placeshield_mouse.response == "responsearea") {
		picture.shield_center.rotation = values.placementAngle;
        return trial.showPE;
	}
	else{
		return trial.placeshield_mouse;
	}
]

</trial>

<text trialcounter>
/items = ("trial: <%values.trialnum%>")
/ position = (80, 15)
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ valign = center
/ halign = center
</text>

<trial showPE> 
/ stimulusframes = [1 = wall, shield_center, cannonball, cannon, trialcounter]
/ trialduration = 1500
/ recorddata = false
/ ontrialbegin = [
	if (values.practiceblock == 5 || values.practiceblock == 0) {
		values.cannon = 2;
	} else {
		values.cannon = 1;
	};
]
/ ontrialend = [
	if (floor(values.placementAngle/360 >= 1)){
		values.placementAngle = values.placementAngle-360;
	} else if (floor(values.placementAngle/360 <= 0)){
		values.placementAngle = values.placementAngle+ 360; 
	 };
]
/ branch = [
		return trial.revealshield
]

</trial>

<trial revealshield>
/ stimulusframes = [1 = wall, shield, cannon, cannonball]
/ validresponse = (57)
/ correctresponse = (57)
/ beginresponsetime = 500
/ trialduration = 2000
/ ontrialbegin = [
	if (values.practiceblock == 5 || values.practiceblock == 0) {
		values.cannon = 2;
	} else {
		values.cannon = 1;
	};
]
/ branch = [
	if (values.outcome <= (values.placementAngle + picture.shield.currentitemnumber) && values.outcome >= (values.placementAngle- picture.shield.currentitemnumber)){
		values.hitmiss = 1;
		values.outcomeindex = 5;
		values.cannonballs_caught += 1;
	} else if (values.placementAngle + picture.shield.currentitemnumber >= 360){
		if (values.outcome <= (values.placementAngle + picture.shield.currentitemnumber - 360)) {
			values.hitmiss = 1;
			values.outcomeindex = 5;
			values.cannonballs_caught += 1;
			}
	} else if (values.placementAngle - picture.shield.currentitemnumber < 0){
		if (values.outcome >= (values.placementAngle- picture.shield.currentitemnumber + 360)){
			values.hitmiss = 1;	
			values.outcomeindex = 5;
			values.cannonballs_caught += 1;
		}
	} else {
		values.cannonballs_missed += 1;
		values.hitmiss = 0;			
		values.outcomeindex = 6;
		};
	trial.cannon_outcome
	]
</trial>

//allow participant to place shield tick mark
//generate shield size
//calculate hit or miss

<item outcome_responses>
 /1 = "You caught the cannonball. Try to miss it!"
 /2 = "You missed the cannonball. Try to catch it!"
 /3 = " Good work, In this case you caught the ball.
 ~n You will now see the full size of your shield after the cannon is shot.
 ~n More than half of the ball must overlap with the shield to make a 'catch'"
 /4 = "Good work. In this case, you missed the ball." 
 /5 = "Ball Caught"
 /6 = "Ball Missed"
</item>


<text Outcome>
/ items = outcome_responses
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ valign = center
/ halign = center
/ vjustify = center
/ select = values.outcomeindex

</text>


<trial cannon_outcome>
/ stimulusframes = [1 = Outcome, Spacetocontinue]
/ recorddata = true
/ correctresponse = (57)
/ timeout = 5000
/ ontrialbegin = [
	if (values.outcomeindex == 2) {
		trial.cannon_outcome.timeout = 1500;	
	} else if (values.outcomeindex == 1) {
		trial.cannon_outcome.timeout = 1500;	
	} else if (values.outcomeindex == 3) {
		trial.cannon_outcome.timeout = 7000;	
	} else if (values.outcomeindex == 6) {
		text.Outcome.textcolor = red;
		text.Spacetocontinue.textcolor = white;
		trial.cannon_outcome.timeout = 500;	
	} else if (values.outcomeindex == 5) {
		text.Outcome.textcolor = green;
		text.Spacetocontinue.textcolor = white;
		trial.cannon_outcome.timeout = 500;		
	};
]

/ branch = [
	if (values.outcomeindex == 2) {
		return trial.Instructions_PlaceShield;
	} else if (values.outcomeindex == 3) {
		 return trial.Instructions_RevealShield;
	} else if (values.outcomeindex == 1) {
		return trial.Instructions_PlaceShield;
	} else if (values.outcomeindex == 6) {
		if (values.practiceblock != 0) {
		 if (values.cannonballs_missed >= 2) {
			return trial.Reminder
			}
		 } 	
	} else if (values.outcomeindex == 5) {
		if (values.practiceblock != 0) {
			if (values.cannonballs_caught < 2){
				return trial.mainloop
			}
		}
	}; 
]   

</trial>


********************************************************************************************
Reset Blocks
********************************************************************************************
<item Howyoudid>
/1 = " In this block you caught <%values.cannonballs_caught%> of the cannonballs and earned <%values.blockearnings%>!"
</item>

<text Howyoudid>
/ items = Howyoudid
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ hjustify = center
/ vjustify = center
/ select = sequence
</text>

<trial Howyoudid>
/ stimulusframes = [1 = Howyoudid, Spacetocontinue]
/ correctresponse = (57)
/ ontrialbegin = [
	values.blockearnings = values.cannonballs_caught*values.centsearned;
	values.totalearnings = values.totalearnings + values.blockearnings;
]
/ ontrialend = [
	values.angmu = expressions.angsurprise;
	values.ang = expressions.outcome;
	values.cannonballs_caught = 0;
	values.cannonballs_missed = 0;
]
</trial>

<item EndPracticePhase>
/1 = "You have completed the round and earned <%values.blockearnings%>!"
/2 = "You have completed the round and earned <%values.blockearnings%>!"
</item>

<text EndPracticePhase>
/ items = EndPracticePhase
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ hjustify = center
/ vjustify = center
/ select =  values.block
</text>

<trial EndPracticePhase>
/ stimulusframes = [1 = EndPracticePhase, Spacetocontinue]
/ correctresponse = (57)
/ ontrialbegin = [
	if (values.practiceblock == 4) {
		values.practiceblock = 5;
	}
	values.blockearnings = values.cannonballs_caught*values.centsearned;
	values.totalearnings = values.totalearnings + values.blockearnings;
]
/ branch = [
	if (values.practiceblock == 5) {
		trial.youwillnotsee;
	} else if (values.practiceblock == 0) {
		trial.start_task;
		}
]
</trial>


<text TotalEarnings>
/ items = ("You have now completed the CANNONBALL TASK. 
~n In total, you earned <%values.totalearnings%> for your performance!
~n press space to continue with the study")
/ fontstyle = ("Arial", 3%, true, false, false, false, 5, 1)
/ position = (50%, 50%)
/ size = (80%, 50%)
/ hjustify = center
/ vjustify = center
</text>

<trial Total_Earnings>
/ stimulusframes = [1 = TotalEarnings, Spacetocontinue]
/ correctresponse = (57)
/ trialduration = 3000
</trial>


**************************************************************************************************************
Experiment Sequence
**************************************************************************************************************	  
<block InstructionBlock>
/ screencolor = aquamarine
/ trials = [
1 = Instructions_BeginTask;
2 = Instructions_PlaceShield;
3-4 = Instructions_RevealShield;
5-7 = mainloop;
8 = PracticeBlock_CannonMoves;
9-10 = PracticeBlock_CannonNotAccurate;
11-13 = mainloop;
14-16 = EndPractice;
]
</block> 

<block begin_round>
/ screencolor = aquamarine
/ onblockbegin = [
	values.trialnum = 0;
	values.practiceblock = 4;
]

/ trials = [
1 = begin_block;
2-3 = mainloop;
4 = EndPracticePhase;
5-6 = mainloop;
7 = start_task;
]

</block>

<block MainBlock>
/ onblockbegin = [
	values.blocknum += 1;
	values.trialnum = 0;
	values.practiceblock = 0;

]
/ trials = [
1-3 = mainloop;
4 = Howyoudid;
]
</block> 

<block Experiment_end>
/ trials = [
1 = Total_Earnings;
]
</block>
1 = InstructionBlock;
<expt>
/ blocks = [ 
1 = InstructionBlock;
2 = begin_round;
3-6 = Mainblock;
7 = begin_round;
8-11 = Mainblock;
12 = Experiment_end;
]

</expt>


